import org.eclipse.fennec.weather.sensinact.util.LocationToGeoJsonBlackbox;
import org.eclipse.fennec.weather.sensinact.util.ToInstantBlackBox;


modeltype ECORE "strict" uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype WEATHER uses "http://cdc.dwd.de/common/weather";
modeltype SENSINACT uses "https://fennec.eclipse.org/sensinact/weather/1.0";
modeltype PROVIDER uses "https://eclipse.org/sensinact/core/provider/1.0";

transformation Weather2Sensinact(in weather : WEATHER, out sensinact : SENSINACT);

main() {
	weather.rootObjects()[WEATHER::MOSMIXSWeatherReport] -> map toSensinactWeatherReport(); 
}

mapping WEATHER::MOSMIXSWeatherReport::toSensinactWeatherReport(): SENSINACT::WeatherProvider {

	id := self.id;
	admin := self.map toAdmin();
	windData := self.map toServiceData(SENSINACT::WindData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::WindData);
	cloudData := self.map toServiceData(SENSINACT::CloudData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::CloudData);
	precipitationData := self.map toServiceData(SENSINACT::PrecipitationData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::PrecipitationData);
	temperatureData := self.map toServiceData(SENSINACT::TemperatureData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::TemperatureData);
	snowRainData := self.map toServiceData(SENSINACT::SnowRainData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::SnowRainData);
	fogData := self.map toServiceData(SENSINACT::FogData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::FogData);
	irradianceData := self.map toServiceData(SENSINACT::IrradianceData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::IrradianceData);
	pressureData := self.map toServiceData(SENSINACT::PressureData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::PressureData);
	visibilityData := self.map toServiceData(SENSINACT::VisibilityData.oclAsType(ECORE::EClass)).oclAsType(SENSINACT::VisibilityData);
	significantWeatherData := self.map toSignificantWeather();
}

mapping WEATHER::MOSMIXSWeatherReport:: toSignificantWeather() : SENSINACT::SignificantWeatherData {
	ww := self.significantWeather3Hours.toString();
	w1w2_w1 := self.significantWeather6Hours.w1.toString();
	w1w2_w2 := self.significantWeather6Hours.w2.toString();
	var ww_meaning : String := WEATHER::WMOWeatherCodeType.oclAsType(ECORE::EEnum).getEEnumLiteralByLiteral(self.significantWeather3Hours.toString()).toDocumentation();
	var w1_meaning : String := WEATHER::WMOWeatherCodeType.oclAsType(ECORE::EEnum).getEEnumLiteralByLiteral(self.significantWeather6Hours.w1.toString()).toDocumentation();
	var w2_meaning : String := WEATHER::WMOWeatherCodeType.oclAsType(ECORE::EEnum).getEEnumLiteralByLiteral(self.significantWeather6Hours.w2.toString()).toDocumentation();
	
	var ww_def : String := WEATHER::MOSMIXSWeatherReport.oclAsType(ECORE::EClass).getEStructuralFeature("significantWeather3Hours").toDocumentation();
	var w1_def : String := WEATHER::W1W2.oclAsType(ECORE::EClass).getEStructuralFeature("w1").toDocumentation();
	var w2_def : String := WEATHER::W1W2.oclAsType(ECORE::EClass).getEStructuralFeature("w2").toDocumentation();
	
	var metadataKeys := Sequence{"description", "sensorthings.unit.definition", "sensorthings.unit.name", "unit", "observation.value.meaning", "dwd.item.id"};
	var ww_metadataValues := Sequence{ww_def, ww_def, ww_def, "00...99", ww_meaning, "ww"};
	var w1_metadataValues := Sequence{w1_def, w1_def, w1_def, "00...99", w1_meaning, "w1w2 (w1 part)"};
	var w2_metadataValues := Sequence{w2_def, w2_def, w2_def, "00...99", w2_meaning, "w1w2 (w2 part)"};
	
	metadata += self.map toGeneralMetadataEntry(SENSINACT::SignificantWeatherData.oclAsType(ECORE::EClass).getEStructuralFeature("ww"), metadataKeys, ww_metadataValues);
	metadata += self.map toGeneralMetadataEntry(SENSINACT::SignificantWeatherData.oclAsType(ECORE::EClass).getEStructuralFeature("w1w2_w1"), metadataKeys, w1_metadataValues);
	metadata += self.map toGeneralMetadataEntry(SENSINACT::SignificantWeatherData.oclAsType(ECORE::EClass).getEStructuralFeature("w1w2_w2"), metadataKeys, w2_metadataValues);
}


query ECORE::EModelElement::toDocumentation(): String {
	return self.eAnnotations->select(a | a.source = "http://www.eclipse.org/emf/2002/GenModel")->first().details->select(d | d.key = "documentation")->first().value;
} 

mapping WEATHER::MOSMIXSWeatherReport::toServiceData(eClass : ECORE::EClass) : PROVIDER::Service {

	init{
		var res := createInstance(eClass);
		eClass.eStructuralFeatures->forEach(feature) {
			res.oclAsType(ECORE::EObject).eSet(feature, self.oclAsType(ECORE::EObject).eGet(WEATHER::MOSMIXSWeatherReport.oclAsType(ECORE::EClass).getEStructuralFeature(feature.name)));
		};	
		var reportEClass : ECORE::EClass := self.oclAsType(ECORE::EObject).eClass();
		eClass.eStructuralFeatures->forEach(f) {
			res.metadata += self.map toMetadataEntry(f, reportEClass.getEStructuralFeature(f.name).toDocumentation());
		};
		result := res;
	}
}

query createInstance(classType : ECORE::EClass) : PROVIDER::Service {
    var res : PROVIDER::Service;
    if (classType != null and classType._abstract.not()) {
        var factory : ECORE::EFactory := classType.ePackage.eFactoryInstance;
        var obj := factory.create(classType);
        res := obj.oclAsType(PROVIDER::Service);
    } else {
        log('Cannot instantiate: class is null or abstract.');
    };
    return res;
}

mapping WEATHER::MOSMIXSWeatherReport::toAdmin(): SENSINACT::WeatherAdmin {
	friendlyName := "Weather Forecast Data from Weather Station " + self.weatherStation.name + " and time " + self.timestamp.toString();	
	weatherStationId := self.weatherStation.id;
	weatherStationName := self.weatherStation.name;
	location := self.weatherStation.location.toGeoJson();
	metadata += self.map toGeneralMetadataEntry(PROVIDER::Admin.oclAsType(ECORE::EClass).getEStructuralFeature("location"), "description", "The location of the weather station");
	metadata += self.map toGeneralMetadataEntry(SENSINACT::WeatherAdmin.oclAsType(ECORE::EClass).getEStructuralFeature("weatherStationId"), "description", "The id of the weather station");
	
}

mapping WEATHER::MOSMIXSWeatherReport::toGeneralMetadataEntry(feature : ECORE::EStructuralFeature, keys : Sequence(String), values : Sequence(String)) : PROVIDER::FeatureMetadata {
	
	key := feature;
	value := self.map toGeneralMetadata(keys, values);	
}

mapping WEATHER::MOSMIXSWeatherReport::toGeneralMetadataEntry(feature : ECORE::EStructuralFeature, propKey : String, propValue : String) : PROVIDER::FeatureMetadata {
	
	key := feature;
	value := self.map toGeneralMetadata(propKey, propValue);	
}

mapping WEATHER::MOSMIXSWeatherReport::toMetadataEntry(feature : ECORE::EStructuralFeature, documentation : String) : PROVIDER::FeatureMetadata {
	
	var unit : String := documentation.substringAfter(":").substringBefore("(");
	var dwdId : String := documentation.substringAfter(":").substringAfter("(").substringBefore(")");
	key := feature;
	value := self.map toMetadata(unit, documentation, dwdId);	
}

mapping WEATHER::MOSMIXSWeatherReport::toMetadata(unit : String, description : String, dwdId : String): PROVIDER::ResourceValueMetadata {
//	extra += toMetadataValueMap("friendlyName", friendlyNameParam); //this will change the name property of the service 
	if(unit != null and unit != ""){
		extra += toMetadataValueMap("unit", unit); //this ends up in unitOfMeasurement.symbol
		extra += toMetadataValueMap("sensorthings.unit.name", unit); //this ends up in unitOfMeasurement.name
	};
	if(dwdId.oclIsUndefined().not()) {
		extra += toMetadataValueMap("dwd.item.id", dwdId);
	};
	if(description.oclIsUndefined().not()){
		extra += toMetadataValueMap("description", description);
		extra += toMetadataValueMap("sensorthings.unit.definition", description); //this ends up in unitOfMeasurement.definition
	};
	timestamp := getENow();
}

mapping WEATHER::MOSMIXSWeatherReport::toMetadataEntry(feature : ECORE::EStructuralFeature, unit : String, description : String): PROVIDER::FeatureMetadata {
	key := feature;
	value := self.map toMetadata(unit, description);
}

mapping WEATHER::MOSMIXSWeatherReport::toGeneralMetadata(propKey : String, propValue : String): PROVIDER::ResourceValueMetadata {
	extra += toMetadataValueMap(propKey, propValue);
}

mapping WEATHER::MOSMIXSWeatherReport::toGeneralMetadata(keys : Sequence(String), values : Sequence(String)): PROVIDER::ResourceValueMetadata {

	var i : Integer := 1;
    while (i <= keys->size()) {
        var key : String := keys->at(i);
        var value : String := values->at(i);
       	extra += toMetadataValueMap(key, value);
        i := i + 1;
    }
    
}

mapping WEATHER::MOSMIXSWeatherReport::toMetadata(unit : String, description : String): PROVIDER::ResourceValueMetadata {
//	extra += toMetadataValueMap("friendlyName", friendlyNameParam); //this will change the name property of the service 
	if(unit != null and unit != ""){
		extra += toMetadataValueMap("unit", unit); //this ends up in unitOfMeasurement.symbol
		extra += toMetadataValueMap("sensorthings.unit.name", unit); //this ends up in unitOfMeasurement.name
	};
//	if(hide.oclIsUndefined().not()){
//		extra += toMetadataValueMap("sensorthings.hide", hide);
//	};
	if(description.oclIsUndefined().not()){
		extra += toMetadataValueMap("description", description);
		extra += toMetadataValueMap("sensorthings.unit.definition", description); //this ends up in unitOfMeasurement.definition
	};
	timestamp := getENow();
}

helper toMetadataValueMap(theKey : String, theValue : String): PROVIDER::MetadataValueMap {
    var entry := object PROVIDER::MetadataValueMap{
       key := theKey;
       value := toMetadataValue(theKey, theValue)
    };
    return entry;
}
helper toMetadataValue(theKey: String, theValue : String): PROVIDER::MetadataValue {
    var entry := object PROVIDER::MetadataValue{
        timestamp := getENow();
        if(theKey = "sensorthings.hide") {
        	value := theValue.toLowerCase() = 'true';
        } else {
        	value := theValue;
        }        
    };
    return entry;
}

query getENow() : EInstant {
    return getNow().oclAsType(EInstant);
}

